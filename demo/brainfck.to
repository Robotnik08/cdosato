define interpret_brainfck (code) {
    const registers = [(long)0] * 10000
    make pointer = 0
    make memory_pointer = 0
    make output = ""

    do {
        make c = code#pointer
        set pointer++
        match c => {
            '>' => {
                set memory_pointer++
            }
            '<' => {
                set memory_pointer--
            }
            '+' => {
                set registers#memory_pointer++
            }
            '-' => {
                set registers#memory_pointer--
            }
            '.' => {
                make char r = registers#memory_pointer
                set output += r
            }
            ',' => {
                // TODO: Implement input
                make char r = listen()#0
                set registers#memory_pointer = r
            }
            '[' => {
                break unless registers#memory_pointer == 0  // Continue execution normally
                
                make loop = 1
                set pointer--
                do {
                    set pointer++
                    make ch = code#pointer
                    if ch == '[' then {
                        set loop++
                    } else if ch == ']' then {
                        set loop--
                    }
                } while loop > 0
            }
            ']' => {
                break unless registers#memory_pointer != 0 // Continue execution normally

                make loop = 1
                set pointer--
                do {
                    set pointer--
                    make ch = code#pointer
                    if ch == '[' then {
                        set loop--
                    } else if ch == ']' then {
                        set loop++
                    }
                } while loop > 0
            }

            other => {
                break
            }
        }
    } while pointer < len(code)

    return output
}

do sayln(interpret_brainfck(
"++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++."
))